{
  config,
  pkgs,
  lib,
  ...
}:

with lib;
let
  cfg = config.my.services.minecraft;
  common = import ./common.nix { inherit pkgs lib; };
in
{
  config = mkIf cfg.enable {
    services.minecraft-servers.servers.lobby = {
      enable = true;
      package = pkgs.paperServers.paper;

      jvmOpts = "-Xms512M -Xmx1G";

      serverProperties = {
        server-port = 25566;
        max-players = 30;
        online-mode = false; # false because Velocity handles authentication
        white-list = false;
        gamemode = "adventure";
        force-gamemode = true;
        difficulty = "peaceful";
        level-type = "flat";
        level-seed = "";
        generator-settings = "{\"layers\": [{\"block\": \"minecraft:air\", \"height\": 1}], \"biome\": \"minecraft:the_void\"}";
        generate-structures = false;
        spawn-monsters = false;
        spawn-animals = false;
        spawn-npcs = false;
        allow-flight = true;
      };

      symlinks = {
        "plugins/ViaVersion.jar" = common.plugins.viaversion.src;
        "plugins/ViaBackwards.jar" = common.plugins.viabackwards.src;
        "plugins/GSit.jar" = common.plugins.gsit.src;
        "plugins/LunaChat.jar" = common.plugins.lunachat.src;
        "velocity-forwarding.secret" = config.sops.secrets.minecraft_forwarding_secret.path;
      };

      files = {
        "plugins/LunaChat/config.yml".value = common.lunachat.config.lunaChatConfig;
        "config/paper-world-defaults.yml".value = {
          entities = {
            spawning = {
              spawn-limits = {
                monsters = 0;
                animals = 0;
                water-animals = 0;
                water-ambient = 0;
                water-underground-creature = 0;
                axolotls = 0;
                ambient = 0;
              };
            };
          };
        };
      };
    };

    systemd.services.minecraft-server-lobby = {
      environment.LD_LIBRARY_PATH = common.ldLibraryPath;

      preStart = ''
        if [ -f ".reset_world" ]; then
          echo "Resetting world and player data as requested..."
          rm -rf world*
          rm -f usercache.json
          rm .reset_world
        fi

        ${common.mkPaperGlobalPreStart {
          secretPath = config.sops.secrets.minecraft_forwarding_secret.path;
        }}
      '';
    };
  };
}
